CMAKE_MINIMUM_REQUIRED(VERSION 3.25 FATAL_ERROR)
PROJECT(fleet-http-client)
INCLUDE(GNUInstallDirs)

SET(FLEET_HTTP_CLIENT_VERSION 1.0.0)
SET(CMAKE_INSTALL_RPATH "$ORIGIN/../${CMAKE_INSTALL_LIBDIR}")
SET(CMAKE_CXX_STANDARD 20)

INCLUDE(CheckPIESupported)
CHECK_PIE_SUPPORTED()
SET(CMAKE_POSITION_INDEPENDENT_CODE ON)

OPTION(BRINGAUTO_TESTS "Enable tests" OFF)
OPTION(BRINGAUTO_PACKAGE "Package creation" OFF)
OPTION(BRINGAUTO_INSTALL "Enable install" OFF)
OPTION(BRINGAUTO_SYSTEM_DEP "Enable system dependencies" OFF)
OPTION(BRINGAUTO_SAMPLES "Enable build of sample app, not used in project" OFF)

IF(BRINGAUTO_PACKAGE)
    IF(NOT BRINGAUTO_INSTALL)
        SET(BRINGAUTO_INSTALL ON CACHE BOOL "Forced install due to BRINGAUTO_PACKAGE=ON" FORCE)
        MESSAGE(WARNING "BRINGAUTO_INSTALL is switched to on because of BRINGAUTO_PACKAGE=ON")
    ENDIF()
ENDIF()

IF(BRINGAUTO_TESTS)
    ENABLE_TESTING()
    INCLUDE(${CMAKE_CURRENT_SOURCE_DIR}/test/CMakeLists.txt)
ENDIF(BRINGAUTO_TESTS)

FIND_PACKAGE(CMLIB
        COMPONENTS CMDEF CMUTIL STORAGE
        REQUIRED
        )

IF(NOT BRINGAUTO_SYSTEM_DEP)
    INCLUDE(cmake/Dependencies.cmake)
ENDIF()

FIND_PACKAGE(libbringauto_logger 1.2.0 REQUIRED)

SET(BUILD_SHARED_LIBS ON)
ADD_SUBDIRECTORY("lib/bringauto-fleet-http-client-generated")

IF(BRINGAUTO_SAMPLES)
    ADD_SUBDIRECTORY(${CMAKE_CURRENT_SOURCE_DIR}/example/)
ENDIF()

FILE(GLOB_RECURSE source_files "src/*")
ADD_LIBRARY(bringautolib STATIC ${source_files})
TARGET_INCLUDE_DIRECTORIES(bringautolib PUBLIC ${CMAKE_CURRENT_LIST_DIR}/include)
TARGET_LINK_LIBRARIES(bringautolib PUBLIC
                        bringauto-fleet-http-client-generated
                        bringauto_logger::bringauto_logger)

CMDEF_ADD_LIBRARY(
        LIBRARY_GROUP fleet-http-client
        TYPE SHARED
        SOURCES ${source_files}
        INCLUDE_DIRECTORIES "$<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/include/>"
        INSTALL_INCLUDE_DIRECTORIES "include/"
        VERSION ${FLEET_HTTP_CLIENT_VERSION}
)

TARGET_LINK_LIBRARIES(fleet-http-client-object PUBLIC bringautolib)
TARGET_LINK_LIBRARIES(fleet-http-client-shared PUBLIC bringautolib)

IF (BRINGAUTO_INSTALL)
    INCLUDE(GNUInstallDirs)
    CMDEF_INSTALL(TARGET fleet-http-client-shared)
ENDIF ()

IF (BRINGAUTO_PACKAGE)
    CMDEF_PACKAGE(
            MAIN_TARGET fleet-http-client-shared
            VERSION ${FLEET_HTTP_CLIENT_VERSION}
    )
    SET(CPACK_GENERATOR ZIP)
    SET(CPACK_PACKAGE_CONTACT "BringAuto s.r.o. <maintainers@bringauto.com>")
    SET(CPACK_DEBIAN_PACKAGE_MAINTAINER "BringAuto s.r.o <maintainers@bringauto.com>")
    SET(CPACK_DEBIAN_PACKAGE_GENERATE_SHLIBS ON)
    SET(CPACK_DEBIAN_PACKAGE_SHLIBDEPS ON)
    INCLUDE(CPack)
ENDIF ()